/************************************************************************************************************/
/* Copyright 2016 DigiPen, All Rights Reserved                                                              */
/* Class: PeakVolumeBar                                                                                     */
/* Brief: The peak volume visualizer for the equalizer tool.                                                */
/*                                                                                                          */
/* Author: Andrea Ellinger                                                                                  */
/************************************************************************************************************/

class PeakVolumeBar : ZilchComponent
{
  [Property] var Bar : CogPath;
  
  var WidgetSize : Real;
  var MaxMove : Real;
  
  function Initialize(init : CogInitializer)
  {
    Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
    
    this.WidgetSize = this.Owner.UiWidget.Size.Y;
    this.MaxMove = this.WidgetSize / 40.0;
    
    this.Bar.Cog.UiWidget.LocalTranslation = Real2(0, this.WidgetSize);
  }

  function OnLogicUpdate(event : UpdateEvent)
  {
    var decibels = this.ToDecibels(Audio.GetPeakOutputLevel());
    var percent = decibels / -42.0;
    if (percent > 1.0)
      percent = 1.0;
      
    var position = this.WidgetSize * percent;
    
    // Is the bar moving down? (0 is at the top)
    if (position > this.Bar.Cog.UiWidget.LocalTranslation.Y)
    {
      // Make it go down slowly
      var difference = position - this.Bar.Cog.UiWidget.LocalTranslation.Y;
      if (difference > this.MaxMove)
        position = this.Bar.Cog.UiWidget.LocalTranslation.Y + this.MaxMove;
    }
      
    this.Bar.Cog.UiWidget.LocalTranslation = Real2(0, position);
  }
    
  function ToDecibels(volume : Real) : Real
  {
    if (volume == 0.0)
      return -100.0;
    
    var decibels = 20.0 * Math.Log10(volume);
    
    return decibels;
  }
}
